      ***************************************************************
      * VERWERK VERRICHTINGEN
      *
      * DIT ZAL ALLE VERRICHTINGEN IN HET BESTAND 'DAGVERRICHTINGEN'
      * VERWERKEN.  HET BESTAND 'REKENINGEN' WORDT DAARBIJ AANGEPAST.
      *
      ***************************************************************
       IDENTIFICATION DIVISION.
       PROGRAM-ID. VERWERK-VERRICHTINGEN.

       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT REKENINGEN ASSIGN TO "BESTANDEN/REKENINGEN"
                  ACCESS MODE IS RANDOM
                  ORGANIZATION IS INDEXED
                  RECORD KEY IS REKNR IN REK.
           SELECT VERRICHTINGEN ASSIGN TO "BESTANDEN/DAGVERRICHTINGEN".

       DATA DIVISION.
       FILE SECTION.
       FD  REKENINGEN BLOCK CONTAINS 10 RECORDS.
       01  REK.
           02 REKNR     PIC 9(12).
           02 KLANTNR   PIC 9(7).
           02 SALDO     PIC 9(7)V99.
           02 GEWIJZIGD PIC 9(8).
           02 RENTE     PIC 9(7)V99.
       FD  VERRICHTINGEN BLOCK CONTAINS 10 RECORDS.
       01  VERR.
           02 SOORT  PIC 9.
               88 OVERSCHRIJVING VALUE 1.
               88 STORTING       VALUE 2.
               88 AFHALING       VALUE 3.
           02 VAN    PIC 9(12).
           02 NAAR   PIC 9(12).
           02 BEDRAG PIC 9(7)V99.
           02 DATUM.
               03 JAAR PIC 9(4).
               03 MAAND PIC 9(2).
               03 DAG PIC 9(2).
           02 MEDEDELING PIC X(50).

       WORKING-STORAGE SECTION.
       77 GEDAAN PIC X(1).
           88 STOP-LEZEN VALUE "J".
       77 LEESRESULTAAT PIC X(1).
           88 LEESFOUT VALUE "J".
           88 GEEN-LEESFOUT VALUE "N".
       01  REK-VAN.
           02 REKNR     PIC 9(12).
           02 KLANTNR   PIC 9(7).
           02 SALDO     PIC 9(7)V99.
           02 GEWIJZIGD PIC 9(8).
           02 RENTE     PIC 9(7)V99.
       01  REK-NAAR.
           02 REKNR     PIC 9(12).
           02 KLANTNR   PIC 9(7).
           02 SALDO     PIC 9(7)V99.
           02 GEWIJZIGD PIC 9(8).
           02 RENTE     PIC 9(7)V99.
       01  REKNR-DISPLAY.
           02 DEEL1 PIC 9(3).
           02 DEEL2 PIC 9(7).
           02 DEEL3 PIC 9(2).
       77  SALDO-DISPLAY PIC Z(7).99.

       PROCEDURE DIVISION.

       MAIN.
           PERFORM INITIALISEER
           PERFORM VOER-VERRICHTING-UIT UNTIL STOP-LEZEN
           PERFORM SLUIT-BESTANDEN
           STOP RUN.

       INITIALISEER.
           MOVE ZEROS TO REK VERR
           OPEN I-O REKENINGEN
           OPEN INPUT VERRICHTINGEN.

       VOER-VERRICHTING-UIT.
           READ VERRICHTINGEN AT END SET STOP-LEZEN TO TRUE
           END-READ
           IF NOT STOP-LEZEN
               IF OVERSCHRIJVING
                   PERFORM DOEOVERSCHRIJVING
               ELSE IF STORTING
                   PERFORM DOESTORTING
               ELSE IF AFHALING
                   PERFORM DOEAFHALING
               END-IF
           END-IF.

       DOEOVERSCHRIJVING.
           DISPLAY "* OVERSCHRIJVING OP " WITH NO ADVANCING
           PERFORM DRUKDATUM
           MOVE VAN TO REKNR-DISPLAY
           DISPLAY "  OPDRACHTGEVER: " WITH NO ADVANCING
           PERFORM DRUKREKNR
           MOVE NAAR TO REKNR-DISPLAY
           DISPLAY "  BEGUNSTIGDE: " WITH NO ADVANCING
           PERFORM DRUKREKNR
           MOVE BEDRAG TO SALDO-DISPLAY
           DISPLAY "  BEDRAG: " SALDO-DISPLAY " EUR"
           DISPLAY "  MEDEDELING: " MEDEDELING
           MOVE VAN TO REKNR IN REK
           SET GEEN-LEESFOUT TO TRUE
           READ REKENINGEN INTO REK-VAN
               INVALID KEY SET LEESFOUT TO TRUE
           END-READ
           MOVE NAAR TO REKNR IN REK
           READ REKENINGEN INTO REK-NAAR
               INVALID KEY SET LEESFOUT TO TRUE
           END-READ
           IF GEEN-LEESFOUT
               SUBTRACT BEDRAG FROM SALDO IN REK-VAN
               ADD BEDRAG TO SALDO IN REK-NAAR
               MOVE DATUM TO GEWIJZIGD IN REK-VAN
               MOVE DATUM TO GEWIJZIGD IN REK-NAAR
               REWRITE REK FROM REK-VAN
               REWRITE REK FROM REK-NAAR
           ELSE
               DISPLAY "FOUT BIJ HET ZOEKEN VAN DE REKENINGNUMMERS!"
           END-IF.

       DOESTORTING.
           DISPLAY "* STORTING OP " WITH NO ADVANCING
           PERFORM DRUKDATUM
           MOVE NAAR TO REKNR-DISPLAY
           DISPLAY "  REKENINGNUMMER: " WITH NO ADVANCING
           PERFORM DRUKREKNR
           MOVE BEDRAG TO SALDO-DISPLAY
           DISPLAY "  BEDRAG: " SALDO-DISPLAY " EUR"
           MOVE NAAR TO REKNR IN REK
           SET GEEN-LEESFOUT TO TRUE
           READ REKENINGEN INTO REK-NAAR
               INVALID KEY SET LEESFOUT TO TRUE
           END-READ
           IF GEEN-LEESFOUT
               ADD BEDRAG TO SALDO IN REK-NAAR
               MOVE DATUM TO GEWIJZIGD IN REK-NAAR
               REWRITE REK FROM REK-NAAR
           ELSE
               DISPLAY "FOUT BIJ HET ZOEKEN VAN HET REKENINGNUMMER!"
           END-IF.

       DOEAFHALING.
           DISPLAY "* AFHALING OP " WITH NO ADVANCING
           PERFORM DRUKDATUM
           MOVE VAN TO REKNR-DISPLAY
           DISPLAY "  REKENINGNUMMER: " WITH NO ADVANCING
           PERFORM DRUKREKNR
           MOVE BEDRAG TO SALDO-DISPLAY
           DISPLAY "  BEDRAG: " SALDO-DISPLAY " EUR"
           MOVE VAN TO REKNR IN REK
           SET GEEN-LEESFOUT TO TRUE
           READ REKENINGEN INTO REK-VAN
               INVALID KEY SET LEESFOUT TO TRUE
           END-READ
           IF GEEN-LEESFOUT
               SUBTRACT BEDRAG FROM SALDO IN REK-VAN
               MOVE DATUM TO GEWIJZIGD IN REK-VAN
               REWRITE REK FROM REK-VAN
           ELSE
               DISPLAY "FOUT BIJ HET ZOEKEN VAN HET REKENINGNUMMER!"
           END-IF.

       SLUIT-BESTANDEN.
           CLOSE REKENINGEN VERRICHTINGEN.

       DRUKDATUM.
           DISPLAY DAG WITH NO ADVANCING
           DISPLAY "/" WITH NO ADVANCING
           DISPLAY MAAND WITH NO ADVANCING
           DISPLAY "/" WITH NO ADVANCING
           DISPLAY JAAR.

       DRUKREKNR.
           DISPLAY DEEL1 WITH NO ADVANCING
           DISPLAY "-" WITH NO ADVANCING
           DISPLAY DEEL2 WITH NO ADVANCING
           DISPLAY "-" WITH NO ADVANCING
           DISPLAY DEEL3.

